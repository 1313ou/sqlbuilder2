plugins {
    id 'org.sqlunet.plugin.querybuilder' version '1.0.0'
    id 'java'
}

group 'org.example'
version '1.0.0'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

test {
    useJUnitPlatform()
}

def generatedSrcDir = new File(buildDir, "generated/sources/java")

sourceSets {

    proto {
    }

    main {
        java.srcDirs += generatedSrcDir
     }
}

def protoDir = sourceSets.proto.resources.srcDirs[0]

querybuilder_args {

    inDir = "${protoDir}/wn"
    factory = "Factory.java"
    variables = ["Names.properties", "NamesExtra.properties"]
    instantiates = ["SqLiteDialect.java", "SqLiteDialect2.java"]

    outDir = "${generatedSrcDir}"
    v = "V"
    q = "Q"
    qv = "QV"
    qPackage = "org.sqlunet.wn"
    instantiateDest = "org.sqlunet.wn"
}

task dumpSourceSets() {
    doLast {
        sourceSets.each { srcSet ->
            println "[" + srcSet.name + "]"
            print "-->Source directories: " + srcSet.allJava.srcDirs + "\n"
            print "-->Resource directories: " + srcSet.resources.srcDirs + "\n"
            print "-->Output Classes directories: " + srcSet.output.classesDirs.files + "\n"
            print "-->Output Resources directories: " + srcSet.output.resourcesDir + "\n"
            print "-->Compile classpath: " + "\n"
            srcSet.compileClasspath.files.each {
                print "  " + it.path + "\n"
            }
            println ""
        }
    }
}

task generateAll {
    dependsOn('generateQV')
            .dependsOn('generateQ')
            .dependsOn('generateV')
            .dependsOn('instantiate')
}

compileJava {
    dependsOn(generateAll)
}
